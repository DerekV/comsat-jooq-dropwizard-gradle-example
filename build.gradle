apply plugin: 'java'
apply plugin: 'application'

group               = 'testgrp'
version             = '0.1.0-SNAPSHOT'
status              = 'integration'

ext.classifier      = ':'      // ':jdk8'
sourceCompatibility = 1.8     // 1.7
targetCompatibility = 1.8     // 1.7

ext.h2Ver           = '1.4.187'
ext.comsatVer       = '0.4.0'
ext.quasarVer       = '0.7.2'
ext.junitVer        = '4.12'
ext.slf4jVer        = '1.7.12'
ext.metricsVer      = '3.1.2'
ext.jdbiVer         = '2.62'
ext.httpCoreVer     = '4.4.1'
ext.guavaVer        = '18.0'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    quasar
    provided
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()

        force "io.dropwizard.metrics:metrics-core:$metricsVer"

        force "com.google.guava:guava:$guavaVer"

        force "org.slf4j:slf4j-api:$slf4jVer"
        force "org.jdbi:jdbi:$jdbiVer"
        force "org.apache.httpcomponents:httpcore:$httpCoreVer"
    }
}

repositories {
//    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "co.paralleluniverse:quasar-core:$quasarVer"
    compile "co.paralleluniverse:comsat-jooq:$comsatVer"
    compile "co.paralleluniverse:comsat-dropwizard:$comsatVer"

    runtime "com.h2database:h2:$h2Ver"

    quasar "co.paralleluniverse:quasar-core:${quasarVer}${classifier}@jar"

    testCompile "junit:junit:$junitVer"
}

mainClassName = "${group}.dw.Main"
applicationDefaultJvmArgs = [
        "-javaagent:${configurations.quasar.singleFile}", // =vdc
        "-Dco.paralleluniverse.fibers.detectRunawayFibers=false",
        "-Dco.paralleluniverse.fibers.verifyInstrumentation=true",
        "-Ddropwizard.config=build/resources/main/conf.yml"
]

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

defaultTasks 'compile'
